S                      = string_lit .
string_lit             = raw_string_lit | interpreted_string_lit .
raw_string_lit         = "`" { unicode_char | newline } "`" .
interpreted_string_lit = `"` { unicode_value | byte_value } `"` .
byte_value             = octal_byte_value | hex_byte_value .
octal_byte_value       = `\` octal_digit octal_digit octal_digit .
hex_byte_value         = `\` "x" hex_digit hex_digit .
little_u_value         = `\` "u" hex_digit hex_digit hex_digit hex_digit .
big_u_value            = `\` "U" hex_digit hex_digit hex_digit hex_digit
                                 hex_digit hex_digit hex_digit hex_digit .

unicode_value          = unicode_char | little_u_value 
                       |  big_u_value | escaped_char .

escaped_char           = `\` ( "a" | "b" | "f" | "n" | "r" 
                       | "t" | "v" | `\` | "'" | `"` ) .

unicode_char           = /* an arbitrary Unicode code point except newline */ .
newline                = /* encoded */ .
